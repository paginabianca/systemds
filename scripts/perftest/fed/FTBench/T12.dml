# Read the datasets
data = read($data, data_type="frame", format="csv", header=FALSE);
spec = read($spec_file, data_type="scalar", value_type="string");

print("("+nrow(data)+", "+ncol(data)+")");

lim = 1;
res = matrix(0, rows=lim, cols=1);
t1 = time();
[X_en, M] = transformencode(target=data, spec=spec);
t2 = time();
res[1,1] = floor((t2-t1)/1000000);

bs = 1024; #batch size
ep = 10;   #epocs
iter_ep = ceil(nrow(data)/bs);
maxiter = ep * iter_ep;
print("Total number of iterations: "+maxiter);
beg = 1;
iter = 0;
i = 1;
V = rand(rows=ncol(data), cols=1, seed=42);

# Mini-batch processing
while (iter < maxiter) {
  end = beg + bs - 1;
  if (end>nrow(data))
    end = nrow(data);

  X_batch = data[beg:end,];
  t1 = time();
  X_batch_en = transformapply(target=X_batch, spec=spec, meta=M);
  # Execute an inexpensive operation
  mx = max(X_batch_en %*% V);
  t2 = time();
  res[1,1] = res[1,1] + floor((t2-t1)/1000000);

  iter = iter + 1;
  if (end == nrow(data)) {
    beg = 1;
    print("Starting next epoch");
  }
  else
    beg = end + 1;
  i = i + 1;
}
print(mx);
print("("+nrow(X_batch_en)+", "+ncol(X_batch_en)+")");
print("Elapsed time for transformations using SystemDS");
print(toString(res));
